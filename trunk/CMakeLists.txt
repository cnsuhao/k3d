PROJECT(k3d)

CMAKE_MINIMUM_REQUIRED(VERSION 2.4 FATAL_ERROR)

SET(CMAKE_HAS_SORT 1)
IF(CMAKE_MAJOR_VERSION EQUAL 2 AND CMAKE_MINOR_VERSION EQUAL 4 AND CMAKE_PATCH_VERSION LESS 4)
	SET(CMAKE_HAS_SORT 0)
ENDIF(CMAKE_MAJOR_VERSION EQUAL 2 AND CMAKE_MINOR_VERSION EQUAL 4 AND CMAKE_PATCH_VERSION LESS 4)

# where to look first for cmake modules, before ${CMAKE_ROOT}/Modules/ is checked
SET(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules")

# Disallow in-source builds
INCLUDE(OutOfSourceBuild)

SET(MAJOR_VERSION 0)
SET(MINOR_VERSION 7)
SET(RELEASE_VERSION 0)
SET(BUILD_VERSION 0)

SET(PACKAGE k3d)
SET(VERSION ${MAJOR_VERSION}.${MINOR_VERSION}.${RELEASE_VERSION}.${BUILD_VERSION})
SET(host ${CMAKE_SYSTEM} ${CMAKE_SYSTEM_PROCESSOR})
SET(COPYRIGHT "Copyright (c) 1995-2007, Timothy M. Shead.  All Rights Reserved.")
SET(prefix ${CMAKE_INSTALL_PREFIX})

INCLUDE(CTest)
ENABLE_TESTING()
MARK_AS_ADVANCED(BUILD_TESTING)
MARK_AS_ADVANCED(DART_TESTING_TIMEOUT)

# Setup high-level build options
IF(WIN32)
	SET(K3D_GLX_DEFAULT OFF)
	SET(K3D_GNOME_DEFAULT OFF)
	SET(K3D_IMAGEMAGICK_DEFAULT OFF)
	SET(K3D_OFFSCREEN_DEFAULT ON)
	SET(K3D_WGL_DEFAULT ON)
	SET(K3D_WINDOWS_DEFAULT ON)
ENDIF(WIN32)

IF(UNIX AND NOT APPLE)
	SET(K3D_GLX_DEFAULT ON)
	SET(K3D_GNOME_DEFAULT ON)
	SET(K3D_IMAGEMAGICK_DEFAULT ON)
	SET(K3D_OFFSCREEN_DEFAULT ON)
	SET(K3D_WGL_DEFAULT OFF)
	SET(K3D_WINDOWS_DEFAULT OFF)
ENDIF(UNIX AND NOT APPLE)

IF(APPLE)
	SET(K3D_GLX_DEFAULT ON)
	SET(K3D_GNOME_DEFAULT OFF)
	SET(K3D_IMAGEMAGICK_DEFAULT OFF)
	SET(K3D_OFFSCREEN_DEFAULT ON)
	SET(K3D_WGL_DEFAULT OFF)
	SET(K3D_WINDOWS_DEFAULT OFF)
ENDIF(APPLE)

OPTION(K3D_BUILD_ANNOTATION_MODULE "Build the annotation plugin module" ON)
OPTION(K3D_BUILD_AQSIS_MODULE "Build the Aqsis render engine integration plugin module" ON)
OPTION(K3D_BUILD_ARRAY_MODULE "Build the array plugin module" ON)
OPTION(K3D_BUILD_BITMAP_MODULE "Build the bitmap plugin module" ON)
OPTION(K3D_BUILD_BLOBBIES_MODULE "Build the blobby plugin module" ON)
OPTION(K3D_BUILD_CORE_MODULE "Build the core plugin module" ON)
OPTION(K3D_BUILD_DEFORMATION_MODULE "Build the mesh deformation plugin module" ON)
OPTION(K3D_BUILD_DEVELOPMENT_MODULE "Build the development plugin module" ON)
OPTION(K3D_BUILD_FREETYPE2_MODULE "Build the freetype2 plugin module" ON)
OPTION(K3D_BUILD_GLX_MODULE "Build the GLX offscreen rendering module" ${K3D_GLX_DEFAULT})
OPTION(K3D_BUILD_GNOME_MODULE "Build the GNOME integration plugin module" ${K3D_GNOME_DEFAULT})
OPTION(K3D_BUILD_GRAPHVIZ_MODULE "Build the GraphViz integration plugin module" ON)
OPTION(K3D_BUILD_GTS_MODULE "Build the GNU Triangulated Surfaces plugin module" OFF)
OPTION(K3D_BUILD_GTS_IO_MODULE "Build the GNU Triangulated Surfaces IO plugin module" ON)
OPTION(K3D_BUILD_IMAGEMAGICK_MODULE "Build the ImageMagick plugin module" ${K3D_IMAGEMAGICK_DEFAULT})
OPTION(K3D_BUILD_JPEG_MODULE "Build the jpeg plugin module" ON)
OPTION(K3D_BUILD_K3DSCRIPT_MODULE "Build the k3dscript scripting language module" ON)
OPTION(K3D_BUILD_K3D_MODULE "Build the K-3D file format module" ON)
OPTION(K3D_BUILD_LSYSTEM_MODULE "Build the l-system parser module" ON)
OPTION(K3D_BUILD_MESH_MODULE "Build the mesh plugin module" ON)
OPTION(K3D_BUILD_MESH_INSTANCE_MODULE "Build the mesh-instance plugin module" ON)
OPTION(K3D_BUILD_NGUI_MODULE "Build the standard graphical user interface plugin" ON)
OPTION(K3D_BUILD_NLS "Build K-3D with native language support" OFF)
OPTION(K3D_BUILD_NUI_MODULE "Build the Null User Interface module" ON)
OPTION(K3D_BUILD_NURBS_MODULE "Build the NURBS plugin module" ON)
OPTION(K3D_BUILD_OBJ_MODULE "Build the Wavefront .obj file format module" ON)
OPTION(K3D_BUILD_OFFSCREEN_MODULE "Build the offscreen OpenGL rendering module" ${K3D_OFFSCREEN_DEFAULT})
OPTION(K3D_BUILD_OPENEXR_MODULE "Build the OpenEXR plugin module" OFF)
OPTION(K3D_BUILD_OPENGL_MODULE "Build the OpenGL integration module" ON)
OPTION(K3D_BUILD_PDIFF_MODULE "Build the perceptual-diff plugin module" ON)
OPTION(K3D_BUILD_PIXIE_MODULE "Build the Pixie render engine integration module" ON)
OPTION(K3D_BUILD_PLY_MODULE "Build the Stanford .ply file format module" ON)
OPTION(K3D_BUILD_PNG_MODULE "Build the png plugin module" ON)
OPTION(K3D_BUILD_PRIMITIVES_MODULE "Build the polygonal primitives module" ON)
OPTION(K3D_BUILD_PYTHON_MODULES "Build Python-related modules" ON)
OPTION(K3D_BUILD_QSLIM_MODULE "Build the qslim module" ON)
OPTION(K3D_BUILD_QTUI_MODULE "Build the sample Qt graphical user interface plugin" OFF)
OPTION(K3D_BUILD_QUADRICS_MODULE "Build the quadrics module" ON)
OPTION(K3D_BUILD_RENDERMAN_MODULE "Build the RenderMan integration module" ON)
OPTION(K3D_BUILD_RENDERMAN_PAINTERS_MODULE "Build the RenderMan mesh painters module" ON)
OPTION(K3D_BUILD_SCRIPTING_MODULE "Build the scripting plugin module" ON)
OPTION(K3D_BUILD_SELECTION_MODULE "Build the selection plugin module" ON)
OPTION(K3D_BUILD_TEST_MODULE "Build the regression test plugin module" ON)
OPTION(K3D_BUILD_TEXTURE_MODULE "Build the texture plugin module" ON)
OPTION(K3D_BUILD_TIFF_MODULE "Build the tiff plugin module" ON)
OPTION(K3D_BUILD_TIME_MODULE "Build the time source plugin module" ON)
OPTION(K3D_BUILD_VIRTUAL_GL_PAINTERS_MODULE "Build virtual OpenGL painters modules" ON)
OPTION(K3D_BUILD_WGL_MODULE "Build the Windows offscreen rendering module" ${K3D_WGL_DEFAULT})
OPTION(K3D_BUILD_WINDOWS_MODULE "Build the Windows integration module" ${K3D_WINDOWS_DEFAULT})
OPTION(K3D_BUILD_YAFRAY_MODULE "Build the YAFRAY render engine integration module" ON)

OPTION(K3D_TEST_VBO "Run tests using VBO painters" OFF)
MARK_AS_ADVANCED(K3D_TEST_VBO)

# Find required external packages
INCLUDE(FindBoost)
IF(NOT K3D_BOOST_FOUND)
	MESSAGE(SEND_ERROR "Couldn't find the boost library")
ENDIF(NOT K3D_BOOST_FOUND)

INCLUDE(FindGlibmm)
IF(NOT K3D_GLIBMM_FOUND)
	MESSAGE(SEND_ERROR "Couldn't find the glibmm library")
ENDIF(NOT K3D_GLIBMM_FOUND)

INCLUDE(FindOpenGL)
IF(NOT OPENGL_FOUND)
	MESSAGE(SEND_ERROR "Couldn't find the OpenGL library")
ENDIF(NOT OPENGL_FOUND)
IF(NOT OPENGL_GLU_FOUND)
	MESSAGE(SEND_ERROR "Couldn't find the OpenGL GLU library")
ENDIF(NOT OPENGL_GLU_FOUND)

INCLUDE(FindSigC)
IF(NOT K3D_SIGC_FOUND)
	MESSAGE(SEND_ERROR "Couldn't find the libsigc++ library")
ENDIF(NOT K3D_SIGC_FOUND)

INCLUDE(FindXML)
IF(NOT K3D_XML_FOUND)
	MESSAGE(SEND_ERROR "Couldn't find an XML parser library")
ENDIF(NOT K3D_XML_FOUND)

INCLUDE(FindZlib)
IF(NOT K3D_ZLIB_FOUND)
	MESSAGE(SEND_ERROR "Couldn't find the zlib library")
ENDIF(NOT K3D_ZLIB_FOUND)

IF(NOT WIN32)
	INCLUDE(FindUUID)
	IF(NOT K3D_UUID_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find uuid library")
	ENDIF(NOT K3D_UUID_FOUND)
ENDIF(NOT WIN32)

# Find optional external packages
IF(K3D_BUILD_FREETYPE2_MODULE)
	INCLUDE(FindFreetype2)
	IF(NOT K3D_FREETYPE2_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the freetype2 library")
	ENDIF(NOT K3D_FREETYPE2_FOUND)
ENDIF(K3D_BUILD_FREETYPE2_MODULE)

IF(K3D_BUILD_GNOME_MODULE)
	INCLUDE(FindGnome)
	IF(NOT K3D_GNOME_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the GNOME libraries")
	ENDIF(NOT K3D_GNOME_FOUND)
ENDIF(K3D_BUILD_GNOME_MODULE)

IF(K3D_BUILD_GTS_MODULE)
	INCLUDE(FindGTS)
	IF(NOT K3D_GTS_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the GTS library")
	ENDIF(NOT K3D_GTS_FOUND)
ENDIF(K3D_BUILD_GTS_MODULE)

IF(K3D_BUILD_IMAGEMAGICK_MODULE)
	INCLUDE(FindImageMagick)
	IF(NOT K3D_IMAGEMAGICK_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the ImageMagick library")
	ENDIF(NOT K3D_IMAGEMAGICK_FOUND)
ENDIF(K3D_BUILD_IMAGEMAGICK_MODULE)

IF(K3D_BUILD_JPEG_MODULE)
	INCLUDE(FindJPEG)
	IF(NOT K3D_JPEG_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the JPEG library")
	ENDIF(NOT K3D_JPEG_FOUND)
ENDIF(K3D_BUILD_JPEG_MODULE)

IF(K3D_BUILD_NGUI_MODULE)
	INCLUDE(FindGtkmm)
	IF(NOT K3D_GTKMM_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the gtkmm library")
	ENDIF(NOT K3D_GTKMM_FOUND)

	INCLUDE(FindGtkGLExt)
	IF(NOT K3D_GTKGLEXT_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the gtkglext library")
	ENDIF(NOT K3D_GTKGLEXT_FOUND)
ENDIF(K3D_BUILD_NGUI_MODULE)

IF(K3D_BUILD_NLS)
	INCLUDE(FindIntl)
	IF(NOT K3D_INTL_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the intl library")
	ENDIF(NOT K3D_INTL_FOUND)
ENDIF(K3D_BUILD_NLS)

IF(K3D_BUILD_OPENEXR_MODULE)
	INCLUDE(FindOpenEXR)
	IF(NOT K3D_OPENEXR_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the OpenEXR library")
	ENDIF(NOT K3D_OPENEXR_FOUND)
ENDIF(K3D_BUILD_OPENEXR_MODULE)

IF(K3D_BUILD_PNG_MODULE)
	INCLUDE(FindPNG)
	IF(NOT K3D_PNG_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the PNG library")
	ENDIF(NOT K3D_PNG_FOUND)
ENDIF(K3D_BUILD_PNG_MODULE)

IF(K3D_BUILD_PYTHON_MODULES)
	INCLUDE(FindPythonLibs)
#	IF(NOT Python_FOUND)
#		MESSAGE(SEND_ERROR "Couldn't find the Python libraries")
#	ENDIF(NOT Python_FOUND)
	MARK_AS_ADVANCED(PYTHON_INCLUDE_PATH)
	MARK_AS_ADVANCED(PYTHON_LIBRARY)
	MARK_AS_ADVANCED(PYTHON_DEBUG_LIBRARY)
ENDIF(K3D_BUILD_PYTHON_MODULES)

IF(K3D_BUILD_QTUI_MODULE)
	INCLUDE(FindQt4)
	IF(NOT QT4_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the Qt4 library")
	ENDIF(NOT QT4_FOUND)

	MARK_AS_ADVANCED(QT_MKSPECS_DIR)
	MARK_AS_ADVANCED(QT_PLUGINS_DIR)
	MARK_AS_ADVANCED(QT_QMAKE_EXECUTABLE)
	MARK_AS_ADVANCED(QT_X11_X11_LIBRARY)
	MARK_AS_ADVANCED(QT_X11_Xext_LIBRARY)
	MARK_AS_ADVANCED(QT_X11_m_LIBRARY)
ENDIF(K3D_BUILD_QTUI_MODULE)

IF(K3D_BUILD_TIFF_MODULE)
	INCLUDE(FindTIFF)
	IF(NOT K3D_TIFF_FOUND)
		MESSAGE(SEND_ERROR "Couldn't find the TIFF library")
	ENDIF(NOT K3D_TIFF_FOUND)
ENDIF(K3D_BUILD_TIFF_MODULE)

######################################################################
# Posix specific configuration

IF(UNIX)
	ADD_DEFINITIONS(-DK3D_PLATFORM_POSIX)
ENDIF(UNIX)

######################################################################
# Win32 specific configuration

IF(WIN32)
	ADD_DEFINITIONS(-DK3D_PLATFORM_WIN32)
	SET(CMAKE_SHARED_LINKER_FLAGS "-Wl,--enable-runtime-pseudo-reloc -Wl,--export-all-symbols" CACHE STRING "" FORCE)
ENDIF(WIN32)

######################################################################
# Mac OS X specific configuration

IF(APPLE)
	ADD_DEFINITIONS(-DK3D_PLATFORM_DARWIN)
ENDIF(APPLE)

SET(EXECUTABLE_OUTPUT_PATH ${k3d_BINARY_DIR}/bin)

IF(WIN32)
	SET(LIBRARY_OUTPUT_PATH ${k3d_BINARY_DIR}/bin)
ELSE(WIN32)
	SET(LIBRARY_OUTPUT_PATH ${k3d_BINARY_DIR}/lib)
ENDIF(WIN32)

IF(WIN32)
	ADD_SUBDIRECTORY(glew)
ENDIF(WIN32)

ADD_SUBDIRECTORY(k3dsdk)

IF(K3D_BUILD_PYTHON_MODULES)
	ADD_SUBDIRECTORY(k3dsdk_python)
ENDIF(K3D_BUILD_PYTHON_MODULES)

ADD_SUBDIRECTORY(subdivision_surface)
ADD_SUBDIRECTORY(surface_polygonizer)
ADD_SUBDIRECTORY(application)
ADD_SUBDIRECTORY(bug-buddy)
ADD_SUBDIRECTORY(make-module-proxy)
ADD_SUBDIRECTORY(renderjob)
ADD_SUBDIRECTORY(renderframe)
ADD_SUBDIRECTORY(sl2xml)
ADD_SUBDIRECTORY(uuidgen)
ADD_SUBDIRECTORY(uimodules)
ADD_SUBDIRECTORY(modules)
ADD_SUBDIRECTORY(share)
ADD_SUBDIRECTORY(tests)
ADD_SUBDIRECTORY(po)
ADD_SUBDIRECTORY(distribution)
ADD_SUBDIRECTORY(docs)
ADD_SUBDIRECTORY(man)

MARK_AS_ADVANCED(CMAKE_BACKWARDS_COMPATIBILITY)
MARK_AS_ADVANCED(EXECUTABLE_OUTPUT_PATH)
MARK_AS_ADVANCED(LIBRARY_OUTPUT_PATH)

# Test to ensure that none of our filenames exceed 90 characters in length (causes portability issues with tar)
FILE(GLOB_RECURSE ALL_SOURCE_FILES RELATIVE ${k3d_SOURCE_DIR} *)
FOREACH(SOURCE_FILE ${ALL_SOURCE_FILES})
	STRING(LENGTH ${SOURCE_FILE} FILENAME_LENGTH)
	IF(FILENAME_LENGTH GREATER 90)
		MESSAGE("The filename \"${SOURCE_FILE}\" is too long (${FILENAME_LENGTH} characters).\nThis can cause portability issues with some versions of tar.")
	ENDIF(FILENAME_LENGTH GREATER 90)
ENDFOREACH(SOURCE_FILE)

