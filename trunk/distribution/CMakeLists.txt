# Setup some odds-and-ends used for distribution
SET(K3D_SVN_ROOT "https://k3d.svn.sourceforge.net:/svnroot/k3d")
SET(K3D_SVN_TRUNK "${K3D_SVN_ROOT}/trunk")
SET(K3D_SVN_TAGS "${K3D_SVN_ROOT}/tags")
SET(K3D_SVN_BRANCHES "${K3D_SVN_ROOT}/branches")
SET(K3D_BRANCH_TAG "k3d-release-${MAJOR_VERSION}.${MINOR_VERSION}")
SET(K3D_VERSION_TAG "k3d-${MAJOR_VERSION}.${MINOR_VERSION}.${RELEASE_VERSION}.${BUILD_VERSION}")

ADD_CUSTOM_TARGET(changelog
	COMMAND ${SVNCOMMAND} log > ${k3d_BINARY_DIR}/ChangeLog
	WORKING_DIRECTORY ${k3d_SOURCE_DIR}
	)

ADD_CUSTOM_TARGET(tag-trunk
	COMMAND ${SVNCOMMAND} cp ${K3D_SVN_TRUNK} ${K3D_SVN_TAGS}/${K3D_VERSION_TAG} -m "COMP: Tagged K-3D Version ${VERSION}"
	WORKING_DIRECTORY ${k3d_SOURCE_DIR}
	)

ADD_CUSTOM_TARGET(tag-branch
	COMMAND ${SVNCOMMAND} cp ${K3D_SVN_TRUNK} ${K3D_SVN_BRANCHES}/${K3D_BRANCH_TAG} -m "COMP: Branched K-3D Version ${VERSION}"
	WORKING_DIRECTORY ${k3d_SOURCE_DIR}
	)

# The Win32 install includes its own copies of GTK and Boost, to prevent DLL hell ...
IF(WIN32)
	INSTALL(FILES ${K3D_BOOST_LIB_DIR}/boost_regex-mgw-1_33_1.dll DESTINATION bin)
	INSTALL(FILES ${K3D_BOOST_LIB_DIR}/boost_python-mgw-1_33_1.dll DESTINATION bin)

	INSTALL(DIRECTORY ${K3D_GTK_DIR}/bin/ DESTINATION bin
		USE_SOURCE_PERMISSIONS
		PATTERN *.pdb EXCLUDE
		PATTERN *-config EXCLUDE
		PATTERN *glade* EXCLUDE
		PATTERN *xml++* EXCLUDE
		PATTERN atkmm-1.6d.dll EXCLUDE
		PATTERN gdkmm-2.4d.dll EXCLUDE
		PATTERN gettext.sh EXCLUDE
		PATTERN glib-gettextize EXCLUDE
		PATTERN glib-mkenums EXCLUDE
		PATTERN glibmm-2.4d.dll EXCLUDE
		PATTERN gtkaio.sh EXCLUDE
		PATTERN gtkautogen EXCLUDE
		PATTERN gtkconfigure EXCLUDE
		PATTERN gtkmm-2.4d.dll EXCLUDE
		PATTERN msg*.exe EXCLUDE
		PATTERN pangomm-1.4d.dll EXCLUDE
		PATTERN sigc-2.0d.dll EXCLUDE
		)

	INSTALL(DIRECTORY ${K3D_GTK_DIR}/etc/ DESTINATION etc
		USE_SOURCE_PERMISSIONS
		)

	INSTALL(DIRECTORY ${K3D_GTK_DIR}/lib/ DESTINATION lib
		USE_SOURCE_PERMISSIONS
		PATTERN *.a EXCLUDE
		PATTERN *.lib EXCLUDE
		PATTERN *.pdb EXCLUDE
		PATTERN *glade* EXCLUDE
		PATTERN gdkmm-2.4 EXCLUDE
		PATTERN glib-2.0 EXCLUDE
		PATTERN glibmm-2.4 EXCLUDE
		PATTERN gtkglext-1.0 EXCLUDE
		PATTERN gtkmm-2.4 EXCLUDE
		PATTERN include EXCLUDE
		PATTERN libxml++-2.6 EXCLUDE
		PATTERN pkgconfig EXCLUDE
		PATTERN sigc++-2.0 EXCLUDE
		)

	# Override the default gtkrc file with our own
	INSTALL(FILES win32/gtkrc DESTINATION etc/gtk-2.0)

ENDIF(WIN32)

# Setup packaging ...
SET(CPACK_GENERATOR "STGZ")
SET(CPACK_PACKAGE_DESCRIPTION "K-3D free-as-in-freedom modeling, animation and rendering system")
SET(CPACK_PACKAGE_EXECUTABLES "k3d;K-3D ${VERSION}")
SET(CPACK_PACKAGE_FILE_NAME "k3d-setup-${VERSION}")
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "K-3D ${VERSION}")
SET(CPACK_PACKAGE_INSTALL_REGISTRY_KEY "K-3D ${VERSION}")
SET(CPACK_PACKAGE_NAME "k3d")
SET(CPACK_PACKAGE_VENDOR "www.k-3d.org")
SET(CPACK_PACKAGE_VERSION ${VERSION})
SET(CPACK_PACKAGE_VERSION_MAJOR ${MAJOR_VERSION})
SET(CPACK_PACKAGE_VERSION_MINOR ${MINOR_VERSION})
SET(CPACK_PACKAGE_VERSION_PATCH ${RELEASE_VERSION})
SET(CPACK_RESOURCE_FILE_LICENSE "${k3d_SOURCE_DIR}/COPYING")
SET(CPACK_SOURCE_GENERATOR "TGZ;TBZ2")
SET(CPACK_SOURCE_PACKAGE_FILE_NAME "k3d-source-${VERSION}")

IF(WIN32)
	STRING(REPLACE "/" "\\\\" NATIVE_CURRENT_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})

	SET(CPACK_GENERATOR "NSIS")
	SET(CPACK_PACKAGE_ICON "${NATIVE_CURRENT_SOURCE_DIR}\\\\win32\\\\nsis\\\\headerimage.bmp")
	SET(CPACK_NSIS_INSTALLED_ICON_NAME "bin\\\\k3d.exe")
	SET(CPACK_NSIS_HELP_LINK "http://www.k-3d.org/wiki/Support")
ENDIF(WIN32)

INCLUDE(CPack)

# Setup the "all-in-one" package ...
IF(WIN32)
	OPTION(K3D_BUILD_ALL_IN_ONE_PACKAGE "Build the K-3D all-in-one package" OFF)

	IF(K3D_BUILD_ALL_IN_ONE_PACKAGE)
		SET(K3D_K3D_INSTALLER "k3d-setup-${VERSION}.exe")
		SET(K3D_K3D_INSTALLER_DIR ${k3d_BINARY_DIR})

		SET(K3D_AQSIS_INSTALLER "aqsis-setup-1.2.0.exe")
		FIND_PATH(K3D_AQSIS_INSTALLER_DIR ${K3D_AQSIS_INSTALLER} DOC "Directory containing ${K3D_AQSIS_INSTALLER}")
		MARK_AS_ADVANCED(K3D_AQSIS_INSTALLER_DIR)

		SET(K3D_PYTHON_INSTALLER "python-2.4.4.msi")
		FIND_PATH(K3D_PYTHON_INSTALLER_DIR ${K3D_PYTHON_INSTALLER} DOC "Directory containing ${K3D_PYTHON_INSTALLER}")
		MARK_AS_ADVANCED(K3D_PYTHON_INSTALLER_DIR)

		SET(K3D_PYGTK_INSTALLER "pygtk-2.8.6-1.win32-py2.4.exe")
		FIND_PATH(K3D_PYGTK_INSTALLER_DIR ${K3D_PYGTK_INSTALLER} DOC "Directory containing ${K3D_PYGTK_INSTALLER}") 
		MARK_AS_ADVANCED(K3D_PYGTK_INSTALLER_DIR)

		SET(K3D_PYCAIRO_INSTALLER "pycairo-1.0.2-1.win32-py2.4.exe")
		FIND_PATH(K3D_PYCAIRO_INSTALLER_DIR ${K3D_PYCAIRO_INSTALLER} DOC "Directory containing ${K3D_PYCAIRO_INSTALLER}")
		MARK_AS_ADVANCED(K3D_PYCAIRO_INSTALLER_DIR)

		SET(K3D_CGKIT_INSTALLER "cgkit-2.0.0alpha7.win32-py2.4.exe")
		FIND_PATH(K3D_CGKIT_INSTALLER_DIR ${K3D_CGKIT_INSTALLER} DOC "Directory containing ${K3D_CGKIT_INSTALLER}")
		MARK_AS_ADVANCED(K3D_CGKIT_INSTALLER_DIR)

		IF(NOT K3D_AQSIS_INSTALLER_DIR)
			MESSAGE(SEND_ERROR "Couldn't locate directory containing ${K3D_AQSIS_INSTALLER}")
		ENDIF(NOT K3D_AQSIS_INSTALLER_DIR)

		IF(NOT K3D_PYTHON_INSTALLER_DIR)
			MESSAGE(SEND_ERROR "Couldn't locate directory containing ${K3D_PYTHON_INSTALLER}")
		ENDIF(NOT K3D_PYTHON_INSTALLER_DIR)

		IF(NOT K3D_PYGTK_INSTALLER_DIR)
			MESSAGE(SEND_ERROR "Couldn't locate directory containing ${K3D_PYGTK_INSTALLER}")
		ENDIF(NOT K3D_PYGTK_INSTALLER_DIR)

		IF(NOT K3D_PYCAIRO_INSTALLER_DIR)
			MESSAGE(SEND_ERROR "Couldn't locate directory containing ${K3D_PYCAIRO_INSTALLER}")
		ENDIF(NOT K3D_PYCAIRO_INSTALLER_DIR)

		IF(NOT K3D_CGKIT_INSTALLER_DIR)
			MESSAGE(SEND_ERROR "Couldn't locate directory containing ${K3D_CGKIT_INSTALLER}")
		ENDIF(NOT K3D_CGKIT_INSTALLER_DIR)

		CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/win32/nsis/all-in-one.nsi.in ${CMAKE_CURRENT_BINARY_DIR}/win32/nsis/all-in-one.nsi @ONLY)

		ADD_CUSTOM_TARGET(package-all-in-one
			makensis ${CMAKE_CURRENT_BINARY_DIR}/win32/nsis/all-in-one.nsi
			)

	ENDIF(K3D_BUILD_ALL_IN_ONE_PACKAGE)
ENDIF(WIN32)
